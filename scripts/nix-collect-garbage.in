#! /usr/bin/perl -w

use strict;
use IPC::Open2;

my $linkdir = "@localstatedir@/nix/links";
my $storedir = "@prefix@/store";

my %alive;

my $keepsuccessors = 1;
my $invert = 0;

foreach my $arg (@ARGV) {
    if ($arg eq "--no-successors") { $keepsuccessors = 0; }
    elsif ($arg eq "--invert") { $invert = 1; }
    else { die "unknown argument `$arg'" };
}

opendir(DIR, $linkdir) or die "cannot open directory $linkdir: $!";
my @links = readdir DIR or die "cannot read directory $linkdir: $!";
closedir DIR;

my @roots;
foreach my $link (@links) {
    $link = $linkdir . "/" . $link;
    next if (!($link =~ /.id$/));
    open ROOT, "<$link" or die "cannot open $link: $!";
    my $root = <ROOT>;
    chomp $root;
    close ROOT;
    push @roots, $root;
}

my $extraarg = "";
if ($keepsuccessors) { $extraarg = "--include-successors"; };
my $pid = open2(\*READ, \*WRITE, "nix-store --query --requisites $extraarg @roots") 
    or die "determining live paths";
close WRITE;
while (<READ>) {
	chomp;
	$alive{$_} = 1;
	if ($invert) { print "$_\n"; };
}
close READ;

waitpid $pid, 0;
$? == 0 or die "determining live paths";

exit 0 if ($invert);

opendir(DIR, $storedir) or die "cannot open directory $storedir: $!";
my @names = readdir DIR;
closedir DIR;

foreach my $name (@names) {
    next if ($name eq "." || $name eq "..");
    $name = "$storedir/$name";
    if (!$alive{$name}) {
        print "$name\n";
    }
}
