#! @perl@ -w -I@libexecdir@/nix

use strict;
use POSIX qw(tmpnam);
use readmanifest;

my $tmpdir;
do { $tmpdir = tmpnam(); }
until mkdir $tmpdir, 0777;

my $manifest = "$tmpdir/manifest";

END { unlink $manifest; rmdir $tmpdir; }

my $binDir = $ENV{"NIX_BIN_DIR"};
$binDir = "@bindir@" unless defined $binDir;

my $libexecDir = $ENV{"NIX_LIBEXEC_DIR"};
$libexecDir = "@libexecdir@" unless defined $libexecDir;

my $stateDir = $ENV{"NIX_STATE_DIR"};
$stateDir = "@localstatedir@/nix" unless defined $stateDir;


# Prevent access problems in shared-stored installations.
umask 0022;


# Process the URLs specified on the command line.
my %narFiles;
my %patches;
my %successors;

sub processURL {
    my $url = shift;

    $url =~ s/\/$//;
    print "obtaining list of Nix archives at $url...\n";

    system("@curl@ --fail --silent --show-error --location --max-redirs 20 " .
           "'$url' > '$manifest'") == 0
           or die "curl failed: $?";

    if (readManifest($manifest, \%narFiles, \%patches, \%successors) < 3) {
        die "manifest `$url' is too old (i.e., for Nix <= 0.7)\n";
    }

    my $baseName = "unnamed";
    if ($url =~ /\/([^\/]+)\/[^\/]+$/) { # get the forelast component
        $baseName = $1;
    }

    my $hash = `$binDir/nix-hash --flat '$manifest'`
        or die "cannot hash `$manifest'";
    chomp $hash;
    
    my $finalPath = "$stateDir/manifests/$baseName-$hash.nixmanifest";
    
    system("mv -f '$manifest' '$finalPath'") == 0
        or die "cannot move `$manifest' to `$finalPath";
}

while (@ARGV) {
    my $url = shift @ARGV;
    processURL $url;
}


my $size = scalar (keys %narFiles);
print "$size store paths in manifest\n";


# Register all substitutes.
print STDERR "registering substitutes...\n";

my $pid = open(WRITE, "|$binDir/nix-store --register-substitutes")
    or die "cannot run nix-store";

foreach my $storePath (keys %narFiles) {
    my $narFileList = $narFiles{$storePath};
    foreach my $narFile (@{$narFileList}) {
        print WRITE "$storePath\n";
        print WRITE "$narFile->{deriver}\n";
        print WRITE "$libexecDir/nix/download-using-manifests.pl\n";
        print WRITE "0\n";
        my @references = split " ", $narFile->{references};
        my $count = scalar @references;
        print WRITE "$count\n";
        foreach my $reference (@references) {
            print WRITE "$reference\n";
        }
    }
}

close WRITE or die "nix-store failed: $?";
